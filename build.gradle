plugins {
    id 'java'
    id 'jacoco'
    id 'io.quarkus' version '3.8.1'
    id "org.sonarqube" version "4.4.1.3373"
    id "com.diffplug.spotless" version '6.25.0'
}

sonarqube {
    properties {
        property "sonar.projectKey", "Ebook-Manager"
        property "sonar.organization", "afrima"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/jacoco-report/jacoco.xml"
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    mavenLocal()
}

spotless {
    java {
        googleJavaFormat('1.20.0')
    }
}

project.ext {
    quarkusPlatformVersion = '3.8.1'
    assertjVersion = '3.25.3'
    testcontainersVersion = '1.19.6'
}

dependencies {
    implementation enforcedPlatform("io.quarkus.platform:quarkus-bom:${quarkusPlatformVersion}")
    implementation "io.quarkus:quarkus-config-yaml"
    implementation "io.quarkus:quarkus-jaxb"
    implementation 'io.quarkus:quarkus-mongodb-panache'
    implementation 'io.quarkus:quarkus-resteasy-reactive-jackson'
  
    implementation "io.quarkus:quarkus-container-image-jib"

    testImplementation "io.quarkus:quarkus-junit5"
    testImplementation "io.quarkus:quarkus-jacoco"
    testImplementation "io.quarkus:quarkus-test-security"
    testImplementation "io.quarkus:quarkus-panache-mock"

    testImplementation "io.rest-assured:rest-assured"
    testImplementation "org.testcontainers:mongodb:$testcontainersVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
}

group 'tech.mathieu'
version hasProperty('release-version') ? properties['release-version'] : 'DEV-SNAPSHOT'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters' << "-Xlint:unchecked" << "-Xlint:deprecation"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    finalizedBy jacocoTestReport
    jacoco {
        excludeClassLoaders = ["*QuarkusClassLoader"]
        destinationFile = layout.buildDirectory.file("jacoco-quarkus.exec").get().asFile
    }
    jacocoTestReport.enabled = false
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}
